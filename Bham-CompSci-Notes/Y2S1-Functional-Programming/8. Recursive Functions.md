We can define functions in terms of themselves, using recursion. We must make sure to always include a base-case.

```haskell
reverse :: [a] -> [a]
reverse []     = []
reverse (x:xs) = reverse xs ++ [x]

length :: [a] -> Int
length []     = 0
length (_:xs) = 1 + length xs

--multiple arguments example
zip :: [a] -> [b] -> [(a,b)]
zip []     _      = []
zip _      []     = []
zip (x:xs) (y:ys) = (x,y) : zip xs ys

drop :: Int -> [a] -> [a]
drop 0 xs     = xs
drop _ []     = []
drop n (_:xs) = drop (n-1) xs

--Multiple Recursion (notice two base cases are needed.)
fib :: Int -> Int
fib 0 = 0
fib 1 = 1
fib n = fib (n-2) + fib (n-1)

--Mutual Recursion, where two or more functions are defined in terms of each other
even :: Int -> Bool
even 0 = True
even n = odd (n-1)
odd :: Int -> Bool
odd 0 = False
odd n = even (n-1)
```
#### Programming Example - Quicksort
The quicksort algorithm for sorting a list of values can be specified by the following two rules:
	-The empty list is already sorted;
	-Non-empty lists can be sorted by sorting the tail values <= the head, sorting the tail values > the head, and then appending the resulting lists on either side of the head value.

![](Images/Pasted%20image%2020231029030529.png)

```haskell
qsort :: Ord a => [a] -> [a]
qsort []     = []
qsort (x:xs) = qsort smaller ++ [x] ++ qsort larger
               where
                 smaller = [a | a <- xs, a <= x]
                 larger  = [b | b <- xs, b > x]
```
