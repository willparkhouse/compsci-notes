We're going to look at two important type classes, `Ord` and `Num`, for ordered and numeric types respectively.  
#### The type `Ordering` and the typeclass `Ord`
The type class `Ord` implements the idea of comparing elements of a type not only for equality, but also for less/greater than. A comparison on a type `a` is a map `compare : a -> a -> Ordering`, where the type `Ordering` is defined as:
```haskell
data  Ordering  =  LT | EQ | GT
          deriving (Eq, Ord, Enum, Read, Show, Bounded)
```

The following defines the `Ord` class for types `a` which are in the `Eq` class.
```haskell
class  (Eq a) => Ord a  where
    compare              :: a -> a -> Ordering
    (<), (<=), (>=), (>) :: a -> a -> Bool
    max, min             :: a -> a -> a

        -- Minimal complete definition:
        --      (<=) or compare
        -- Using compare can be more efficient for complex types.
    compare x y
         | x == y    =  EQ
         | x <= y    =  LT
         | otherwise =  GT

    x <= y           =  compare x y /= GT
    x <  y           =  compare x y == LT
    x >= y           =  compare x y /= LT
    x >  y           =  compare x y == GT

-- note that (min x y, max x y) = (x,y) or (y,x)
    max x y
         | x <= y    =  y
         | otherwise =  x
    min x y
         | x <= y    =  x
         | otherwise =  y
```
There is a seeming circularity between the definition of `Ordering` and `Ord`, in that each one refers to the other. We may see this as a **mutually recursive definition**.

We implement the `compare` function on lists of `a` like:
```haskell
instance (Ord a) => Ord [a] where
  compare [] []         = EQ
  compare [] (_:_)      = LT
  compare (_:_) []      = GT
  compare (x:xs) (y:ys) = case compare x y of
                              EQ    -> compare xs ys
                              other -> other
```

#### The type class `Num`
```haskell
Prelude> 5 + 3
8
Prelude> 3.14159 + 2.71828
5.85987
```
In the above, the `+` operates on any type that is an instance of the `Num` typeclass. 

```haskell
Prelude> :info Num
class Num a where
  (+) :: a -> a -> a
  (-) :: a -> a -> a
  (*) :: a -> a -> a
  negate :: a -> a
  abs :: a -> a
  signum :: a -> a
  fromInteger :: Integer -> a
  {-# MINIMAL (+), (*), abs, signum, fromInteger, (negate | (-)) #-}
  	-- Defined in ‘GHC.Num’
instance Num Word -- Defined in ‘GHC.Num’
instance Num Integer -- Defined in ‘GHC.Num’
instance Num Int -- Defined in ‘GHC.Num’
instance Num Float -- Defined in ‘GHC.Float’
instance Num Double -- Defined in ‘GHC.Float’
```
We can see that Num is applicable to `Word`, `Integer`, `Int`, `Float`, and `Double` types.

Consider `1`, this is could be an Int, Word, Float, etc.., so we ask `ghci` to infer the type of `1` for us, then `ghci` just needs to check that `Word` is an instance of `Num`.
```haskell
Prelude> :type 1
1 :: Num p => p

Prelude> :type 1 :: Word
1 :: Word :: Word
```

Consider the Haskell code below:
```haskell
f1, f2, f3 :: Bool -> Bool
f1 x = if x then False else True
f2 x = not x
f3 x = x

Prelude> f1 == f2
<interactive>:14:1: error:
```
We can see that we can't equate f1 and f2, as there maybe be infinitely many `x` inputs, so it's impossible to verify. From Computability Theory, we know that, unfortunately, there is no algorithm that can decide whether or not two given functions are equal, in general.

We can implement an instance to make this comparison work though, as we are only looking at functions from `Bool -> Bool`:
```haskell
instance Eq (Bool -> Bool) where
  f == g = f True == g True && f False == g False
```

`f == g`: This is the function comparison. It states that two functions are equal if their behaviour is the same for all possible inputs.

`f True == g True && f False == g False`: This part checks whether the two functions are equivalent for both possible input values, `True` and `False`.

`f True == g True` compares the result of applying `f` to `True` with the result of applying `g` to `True`. `f False == g False` similarly compares the results of applying `f` and `g` to `False`. 

Now our function can successfully be compared for equality:
```haskell
*Main> f1 == f2
True
*Main> f1 == f3
False
```

But we can make this more general:
```haskell
instance Eq a => Eq (Bool -> a) where
  f == g = f True == g True && f False == g False
```
In English, this says the following:
 * If we know how to compare elements of the type `a` for equality,
 * then we can compare functions `f,g :: Bool -> a` for equality using the algorithm `f True == g True && f False == g False`.