A function is called `higher-order` if it takes a **function** as an **argument** or **returns** a function as a **result**. Example:
```haskell
twice :: (a -> a) -> a -> a
twice f x = f (f x)

> twice (*2) 3
12
```
#### The `map` Function
The library function called `map` applies a function to every element of a list.
```haskell
map :: (a -> b) -> [a] -> [b]

--Examples
> map (+1) [1,3,5,7]
[2,4,6,8]
> map (^3) [1,3,5,7]
[1,27,125,343]
> map reverse ["conversation", "talking", "discussion"]
["noitasrevnoc","gniklat","noissucsid"]

--We could define the map function using a list comprehension:
map :: (a -> b) -> [a] -> [b]
map f xs = [f x | x <- xs]

--or alternatively, via recursion
map :: (a -> b) -> [a] -> [b]
map f []     = []
map f (x:xs) = f x : map f xs
```

#### The `filter` Function
The library function `filter` selects every element from a list that satisfies a predicate.
```haskell
filter :: (a -> Bool) -> [a] -> [a]

--Example
> filter even [1..10]
[2,4,6,8,10]

--Again, we could define via list comprehension:
filter :: (a -> Bool) -> [a] -> [a]
filter p xs = [x | x <- xs, p x]

--or via recursion
filter :: (a -> Bool) -> [a] -> [a]
filter p [] = []
filter p (x:xs)
   | p x       = x : filter p xs
   | otherwise = filter p xs
```

#### The `foldr` Function
Lots of functions on lists can be defined by the following pattern:
```haskell
f :: Num a => [a] -> a
f []     = v
f (x:xs) = x # f xs
--The function `f` maps the empty list to some value `v`, and any non-empty list to some function `#` applied to its head and `f` of its tail.

--Examples
sum :: Num a => [a] -> a
sum []     = 0
sum (x:xs) = x + sum xs

product :: Num a => [a] -> a
product []     = 1
product (x:xs) = x * product xs

and :: [Bool] -> Bool
and []     = True
and (x:xs) = x && and xs
```

The higher-order library function `foldr` (_fold right_) encapsulates this simple pattern of recursion, with the function `#` and the value `v` as arguments.
```haskell
sum = foldr (+) 0
product = foldr (*) 1
and = foldr (&&) True
or = foldr (||) False
```

We can define `foldr` with recursion:
```haskell
foldr :: (a -> b -> b) -> b -> [a] -> b
foldr f v []     = v
foldr f v (x:xs) = f x (foldr f v xs)
```

Yet, it's instead best to think of `foldr` as simultaneously replacing each (`:`) in a list by a given function, and `[]` by a given value:
```haskell
--Example uses
sum [1,2,3]
= foldr (+) 0 [1,2,3]
= foldr (+) 0 (1:(2:(3:[])))
= 1+(2+(3+0))
= 6

product [1,2,3]
= foldr (*) 1 [1,2,3]
= foldr (*) 1 (1:(2:(3:[])))
= 1*(2*(3*1))
= 6
```

Recall the `length` definition:
```haskell
length :: [a] -> Int
length []     = 0
length (_:xs) = 1 + length xs

--Representation of internal steps--
length [1,2,3]
= length (1:(2:(3:[])))
= 1+(1+(1+0))
= 3
```
we can replace each (:) by `\_ n -> 1+n` and [] by `0` to get:
```haskell
length :: [a] -> Int
length = foldr (\_ n -> 1+n) 0
```
`\_ n -> 1+n` is a lambda function that takes the wildcard input `_` and adds one to `n`, which starts at `0`, and then `foldr` does this for every element in the list going to the right. 

We can define `reverse` using `foldr`:
```haskell
reverse :: [a] -> [a]
reverse = foldr (\x xs -> xs ++ [x]) []
```

Even the append function `(++)` has a very terse definition, using `foldr:
```haskell
(++ ys) = foldr (:) ys

--even more terse
(++) = foldr (:)
```

#### The `foldl` Function
`foldl` is the same as `foldr`, apart from `foldl` associates to the left, whereas `foldr` associates to the right. 
```haskell
--SUM example, but with left associativity
sum :: Num a => [a] -> a
sum = sum' 0
      where
         sum' v []     = v
         sum' v (x:xs) = sum' (v+x) xs

--Result
sum [1,2,3]
= sum' 0 [1,2,3]
= sum' (0+1) [2,3]
= sum' ((0+1)+2) [3]
= sum' (((0+1)+2)+3) []
= (((0+1)+2)+3)
```

Generalizing from the above `sum` example, many functions on lists can be defined using the following simple pattern of recursion:
```haskell
f :: Num a => a -> [a] -> a
f v []     = v
f v (x:xs) = f (v # x) xs
```
The function maps the empty list to the _accumulator_ value `v`, and any non-empty list to the result of recursively processing the tail using a new accumulator value obtained by applying an operator `#` to the current value and the head of the list.

The above `sum` function can be re-written using the higher-order library function `foldl` (_fold left_) as:
```haskell
--FOLDL
sum :: Num a => [a] -> a
sum = foldl (+) 0
```

#### The Composition Operator
The library function (.) returns the composition of two functions as a single function.
```haskell
(.) :: (b -> c) -> (a -> b) -> (a -> c)
f . g = \x -> f (g x)
```
For example:
```haskell
odd :: Int -> Bool
odd n = not (even n)
```
Can be defined as:
```haskell
odd :: Int -> Bool
odd = not . even
```

## Programming Example - Binary String Transmitter
A binary number is a sequence of zeros and ones, called _bits_, in which successive bits as we move to the left increase in weight by a factor of two.

To simplify the definition of certain functions, we assume for the remainder of this example that binary numbers are written in _reverse_ order. For example, `1101` would now be written as `1011`, with successive bits as we move to the right increasing in weight by a factor of two:
```haskell
1011 = (1 * 1) + (2 * 0) + (4 * 1) + (8 * 1)
```
#### Base Conversion
We begin by importing the library of useful functions on characters and declaring the type `Bit` 
```haskell
import Data.Char
type Bit = Int
```

We can convert a binary number, represented as a list of bits, to an integer using the `bin2int` function. Consider an arbitrary four-bit number `[a,b,c,d]`. We can deconstruct this, producing the following weighted sum:
```haskell
(1 * a) + (2 * b) + (4 * c) + (8 * d)

---which can be restructured as follows:
(1 * a) + (2 * b) + (4 * c) + (8 * d)
= a + (2 * b) + (4 * c) + (8 * d)
= a + 2 * (b + (2 * c) + (4 * d))
= a + 2 * (b + 2 * (c + (2 * d)))
= a + 2 * (b + 2 * (c + 2 * (d + 2 * 0)))
```

We see that the conversion can be written as replacing each `cons` by the function that adds its first argument to twice the second argument, and replacing the empty list by zero. Therefore, `bin2int` can be written as:
```haskell
bin2int :: [Bit] -> Int
bin2int = foldr (\x y -> x + 2*y) 0

--Result
> bin2int [1,0,1,1]
13
```

The opposite function for converting a non-negative integer into binary number can be written as:
```haskell
int2bin :: Int -> [Bit]
int2bin 0 = []
int2bin n = n `mod` 2 : int2bin (n `div` 2)

--Result
> int2bin 13
[1,0,1,1]
```

We can now define a function `make8` that ensures we have binary numbers of the same length i.e. 8 bits. It either truncates or extends a binary number as appropriate to make it 8 bits long:
```haskell
make8 :: [Bit] -> [Bit]
make8 bits = take 8 (bits ++ repeat 0)

--Result
> make8 [1,0,1,1]
[1,0,1,1,0,0,0,0]
```
#### Transmission
We can now define a function that encodes a string of characters as a list of bits by converting each character into a Unicode number, converting each such number into an eight-bit binary number, and concatenating each of these numbers together to produce a list of bits.
```haskell
encode :: String -> [Bit]
encode = concat . map (make8 . int2bin . ord)

--Result
> encode "abc"
[1,0,0,0,0,1,1,0,0,1,0,0,0,1,1,0,1,1,0,0,0,1,1,0]
```

To decode a list of bits, we firstly define a function `chop8` that chops such a list up into eight-bit binary numbers:
```haskell
chop8 :: [Bit] -> [[Bit]]
chop8 [] = []
chop8 bits = take 8 bits : chop8 (drop 8 bits)
```

Now we can define the `decode` function that chops a list of bits, converts each resulting binary number into a Unicode number and then a character:
```haskell
decode :: [Bit] -> String
decode = map (chr . bin2int) . chop8

--Result
> decode [1,0,0,0,0,1,1,0,0,1,0,0,0,1,1,0,1,1,0,0,0,1,1,0]
"abc"
```

Finally, we can define the function `transmit` that simulates the transmission of a string of characters as a list of bits, using a perfect communication channel that we model using the identity function:
```haskell
transmit :: String -> String
transmit = decode . channel . encode

channel :: [Bit] -> [Bit]
channel = id

--Results
> transmit "higher-order functions are easy"
"higher-order functions are easy"
```

The above example is actually encapsulating three functions i.e. encoding, transmission and decoding. We can separate the encoding and decoding steps to see what happens in between. The channels is an identity function, i.e. it outputs whatever is given to it as input.

``` haskell
> encode "higher-order functions are easy"
[0,0,0,1,0,1,1,0,1,0,0,1,0,1,1,0,1,1,1,0,0,1,1,0,0,0,0,1,0,1,1,0,1,0,1,0,0,1,1,0,0,1,0,0,1,1,1,0,1,0,1,1,0,1,0,0,1,1,1,1,0,1,1,0,0,1,0,0,1,1,1,0,0,0,1,0,0,1,1,0,1,0,1,0,0,1,1,0,0,1,0,0,1,1,1,0,0,0,0,0,0,1,0,0,0,1,1,0,0,1,1,0,1,0,1,0,1,1,1,0,0,1,1,1,0,1,1,0,1,1,0,0,0,1,1,0,0,0,1,0,1,1,1,0,1,0,0,1,0,1,1,0,1,1,1,1,0,1,1,0,0,1,1,1,0,1,1,0,1,1,0,0,1,1,1,0,0,0,0,0,0,1,0,0,1,0,0,0,0,1,1,0,0,1,0,0,1,1,1,0,1,0,1,0,0,1,1,0,0,0,0,0,0,1,0,0,1,0,1,0,0,1,1,0,1,0,0,0,0,1,1,0,1,1,0,0,1,1,1,0,1,0,0,1,1,1,1,0]

> decode [0,0,0,1,0,1,1,0,1,0,0,1,0,1,1,0,1,1,1,0,0,1,1,0,0,0,0,1,0,1,1,0,1,0,1,0,0,1,1,0,0,1,0,0,1,1,1,0,1,0,1,1,0,1,0,0,1,1,1,1,0,1,1,0,0,1,0,0,1,1,1,0,0,0,1,0,0,1,1,0,1,0,1,0,0,1,1,0,0,1,0,0,1,1,1,0,0,0,0,0,0,1,0,0,0,1,1,0,0,1,1,0,1,0,1,0,1,1,1,0,0,1,1,1,0,1,1,0,1,1,0,0,0,1,1,0,0,0,1,0,1,1,1,0,1,0,0,1,0,1,1,0,1,1,1,1,0,1,1,0,0,1,1,1,0,1,1,0,1,1,0,0,1,1,1,0,0,0,0,0,0,1,0,0,1,0,0,0,0,1,1,0,0,1,0,0,1,1,1,0,1,0,1,0,0,1,1,0,0,0,0,0,0,1,0,0,1,0,1,0,0,1,1,0,1,0,0,0,0,1,1,0,1,1,0,0,1,1,1,0,1,0,0,1,1,1,1,0]
"higher-order functions are easy"
```
