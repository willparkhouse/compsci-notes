c$\{x^2|x\in\{1...5\}\}$ produces the set {1, 4, 9, 16, 25} of all numbers x^2 such that x is an element of the set {1...5}.

In Haskell, a similar comprehension notation can be used to construct __new lists__ from old lists. For example: 
```haskell
> [x^2 | x <- [1..5]] 
[1,4,9,16,25]
```
The symbol `|` is read as _such that_, `<-` is read as **_drawn from_**, and the expression `x <- [1..5]` is called a __generator__. A generator states how to generate values for x. We can have multiple generators:
```haskell
--This list iterates through the [1,2,3] of x first:
> [(x,y) | x <- [1,2,3], y <- [4,5]] 
[(1,4),(1,5),(2,4),(2,5),(3,4),(3,5)]
--ORDER MATTERS, this list iterates through the [4,5] of Y first:
> [(x,y) | y <- [4,5], x <- [1,2,3]]
[(1,4),(2,4),(3,4),(1,5),(2,5),(3,5)]
```
Multiple generators are like nested loops, with later generators as more deeply nested loops whose variables change value more frequently.

We can have generators that depends on the variables that are introduced by earlier generators:
```haskell
> [(x,y) | x <- [1..3], y <- [x..3]]
[(1,1),(1,2),(1,3),(2,2),(2,3),(3,3)]
```

Using a dependent generator we can define the library function that _concatenates_ a list of lists:
```haskell
concat :: [[a]] -> [a]
concat xss = [x | xs <- xss, x <- xs]
```

The wildcard pattern `_` is sometimes useful in generators to discard certain elements from a list. For example, a function that selects all the first components from a list of pairs can be defined as follows:
```haskell
firsts :: [(a,b)] -> [a]
firsts ps = [x | (x, _) <- ps]
```


#### Guards
List comprehensions can use **guards** to restrict the values produced by earlier generators. If the guard is _true_, then the current values are retained; if it is _false_, then they are discarded.

Examples
```haskell
> [x | x <- [1..10], even x]
[2,4,6,8,10]

factors :: Int -> [Int]
factors n = [x | x <- [1..n], n `mod` x == 0]
> factors 15
[1,3,5,15]
```

A positive integer is prime if its only factors are 1 and itself. Hence, using `factors` we can define a function that decides if a number is prime:
```haskell
prime :: Int -> Bool
prime n = factors n == [1,n]
```

Using a guard we can now define a function that returns the list of all primes up to a given limit:
```haskell
primes :: Int -> [Int]
primes n = [x | x <- [2..n], prime x]
```

#### Zip Function
A useful library function is `zip`, which maps two lists to a list of pairs of their corresponding elements.

```haskell
zip :: [a] -> [b] -> [(a,b)]


> zip ['a','b','c'] [1,2,3,4]
[('a',1),('b',2),('c',3)]

--function that returns the list of all pairs of adjacent elements
pairs :: [a] -> [(a,a)]
pairs xs = zip xs (tail xs)
> pairs [1,2,3,4]
[(1,2),(2,3),(3,4)]

--function that decides if the elements in a list are sorted
sorted :: Ord a => [a] -> Bool
sorted xs = and [x <= y | (x,y) <- pairs xs]
> sorted [1,2,3,4]
True
> sorted [1,3,2,4]
False

--function that returns the list of all positions of a value in a list
positions :: Eq a => a -> [a] -> [Int]
positions x xs =
   [i | (x',i) <- zip xs [0..], x == x']
> positions 0 [1,0,0,1,0,1,1,0]
[1,2,4,7]
> positions False [True, False, True, False]
[1,3]
```