Functional vs Imperative

In imperative languages, lots of things are dynamic and changeable. Functional programming is a programming paradigm that treats computation as the evaluation of mathematical functions and avoids changing state and mutable data. It emphasizes immutability, first-class and higher-order functions, and declarative programming.

Lots of other 'non-functional' languages, like Python, Kotlin and Java, include functional features. 

**Conway's Game of Life**

We are going to look at the 'Game of Life' as an example program to familiarise ourselves with Haskell notation, convention and thinking. 

The _Game of Life_ begins by placing some tokens on the cells of a two-dimensional grid. Cells of the grid that contain a token are said to be _live_, while empty cells are said to be _dead_. Each cell has exactly eight neighbours, corresponding to the cells that are immediately adjacent horizontally, vertically, or diagonally.

The initial configuration is called the _seed_, or generation 0. Afterwards the game is played deterministically according to the following rules of evolution whereby a cell interacts with its neighbours:

1. (Birth) A dead cell with exactly three live neighbours becomes live at the next generation.
2. (Survival) A live cell with either two or three live neighbours survives to the next generation.
3. (Death) A cell dies from overcrowding if it has more than three live neighbours, or from isolation if it has fewer than two neighbours.

The first thing we need to do is define our state-space. We are going to define a cell as an int, int 'pair'. A pair is defined by using parentheses. We also need to define our grid, which we will use the 'list' type, this is defined by square brackets:

```haskell
type Cell = (Int,Int)
type Grid = [Cell]
```

We are going to define our first functions:

```haskell
isLive, isDead :: Cell -> Grid -> Bool
isLive c g = c `elem` g
isDead c g = not (isLive c g)
```

We use the notation '::' to ascribe a type to an expression, in this case the functions `isLive` and `isDead`. The final type in the chain of `->`'s is the output, and the preceding types are the arguments this function takes. Bool is the output of `isLive` and `isDead`. We are saying that `isLive` and `isDead` are created and are function that take a `Cell` and a `Grid` and return a `Bool`.  `isLive` is true if `c` is an element of `g`. `isDead` is true if `c` is not `isLive`.

```haskell
neighbours :: Cell -> [Cell]
neighbours (x,y) = [ (x+i,y+j) | i <- [-1..1], j <- [-1..1], not (i==0 && j==0) ]
```

The above is a function that generate a list of cells, using what's called a 'list comprehension'. It uses set notation to generate these lists, but with rigorous syntax rules.

```haskell
liveNeighbours :: Grid -> Cell -> [Cell]
liveNeighbours g c = [c' | c' <- neighbours c,isLive c' g]
```

The above is a function that takes the grid, a specific cell and using the functions we previously defined it can form a list of `liveNeighbours`. 

We now need to have a function that computes the evolution of each step of the game:
```haskell
step :: Grid -> Grid
step [] = []
step g =
  [(x,y) | x <- [minX-1 .. maxX+1],
           y <- [minY-1 .. maxY+1],
              (isDead (x,y) g && length (liveNeighbours g (x,y)) == 3)
           || (isLive (x,y) g && length (liveNeighbours g (x,y)) `elem` [2,3])
         ]
  where
    minX = minimum [ x | (x,y) <- g ]
    maxX = maximum [ x | (x,y) <- g ]
    minY = minimum [ y | (x,y) <- g ]
    maxY = maximum [ y | (x,y) <- g ]
```
Firstly, the bottom 4 `where` conditions define a bounding box of where the live cells are currently, the min and max of X and Y. (This all happens in the `where` block, which is evaluated first even though it occurs syntactically below the list comprehension.) The `step` function then finds the bounding box of where live cells *could* be after the first iteration. For all the cells lying within this region, we then test the Boolean formula:
```haskell
   (isDead (x,y) g && length (liveNeighbours g (x,y)) == 3)
|| (isLive (x,y) g && length (liveNeighbours g (x,y)) `elem` [2,3])
```

Notice we also have `step [] = []`, which covers the case of an empty grid, that can never be repopulated. It is important that the clause `step [] = []` appears before the clause`step g = {- ... -}`, otherwise the implementation would be incorrect, since the operations `minimum` and `maximum` raise an exception in the case of an empty list.
